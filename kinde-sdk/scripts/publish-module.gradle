apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = sdk_version // Set up version in build.gradle at project's root

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // Pure kotlin libraries
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version sdk_version

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Kinde Android SDK'
                    url = 'https://github.com/kinde-oss/kinde-sdk-android'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/kinde-oss/kinde-sdk-android/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'kindeengineering'
                            name = 'Kinde Engineering'
                            email = 'engineering@kinde.com'
                        }
                    }
                scm {
                        connection = 'scm:git:github.com/kinde-oss/kinde-sdk-android.git'
                        developerConnection = 'scm:git:ssh://github.com/kinde-oss/kinde-sdk-android.git'
                        url = 'https://github.com/kinde-oss/kinde-sdk-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}